plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

Set<String> apiModules = [
        'fabric-resource-loader-v0',
		'fabric-rendering-v1'
]

repositories {
	mavenCentral()
	maven {
		name = 'Quilt'
		url = 'https://maven.quiltmc.org/repository/release'
	}
	maven {
		name = 'Quilt Snapshots'
		url = 'https://maven.quiltmc.org/repository/snapshot'
	}

	maven {
		name = 'Gegy'
		url = 'https://maven.gegy.dev'
	}

	maven {
		name = 'TerraformersMC'
		url = 'https://maven.terraformersmc.com'
	}
}

sourceSets {
	testmod {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "org.quiltmc:quilt-mappings:${project.minecraft_version}+build.${project.quilt_build_number}:intermediary-v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// The bundled version of SpruceUI contains the necessary FAPI modules, and there is no reason to include them twice.
	//modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	apiModules.forEach {
		include modImplementation(fabricApi.module(it, project.fabric_version))
	}

	include modImplementation("dev.lambdaurora:spruceui:${project.spruceui_version}")
	include implementation("com.electronwill.night-config:core:${project.night_config_version}")
	include implementation("com.electronwill.night-config:toml:${project.night_config_version}")

	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	testmodImplementation sourceSets.main.output
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = 'cactus-config'

			from components.java

			pom {
				name = 'Cactus Config'
				description = 'A toml based configuration library for Minecraft.'
			}
		}
	}

	repositories {
		maven {
			name = 'cloudsmith'
			url = 'https://maven.cloudsmith.io/carbon-toast/cactus-config/'
			def releasesRepoUrl = 'https://maven.cloudsmith.io/carbon-toast/cactus-config/'
			def snapshotsRepoUrl = 'https://maven.cloudsmith.io/carbon-toast/cactus-config/'
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			credentials {
				username = System.getenv('USERNAME')
				password = System.getenv('API_KEY')
			}
		}
	}
}
